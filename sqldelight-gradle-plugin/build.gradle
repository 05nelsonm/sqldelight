apply plugin: 'kotlin'
apply plugin: 'idea'

configurations {
  provided
}

sourceSets {
  main.compileClasspath += configurations.provided
}

idea {
  module {
    scopes.PROVIDED.plus += [configurations.provided]
  }
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

test {
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
    showExceptions true
    showStackTraces true
    showCauses true
  }
}

task createClasspathManifest {
  def outputDir = file("$buildDir/$name")

  inputs.files sourceSets.main.runtimeClasspath
  outputs.dir outputDir

  doLast {
    outputDir.mkdirs()
    file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
  }
}

dependencies {
  provided gradleApi()
  compile project(':sqldelight-compiler')
  compile dep.kotlinStdLib
  compile dep.androidPlugin

  testCompile gradleTestKit()
  testCompile dep.junit
  testCompile dep.truth
  testRuntime files(createClasspathManifest)
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
